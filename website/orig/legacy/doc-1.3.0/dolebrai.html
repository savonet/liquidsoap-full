<?xml version="1.0" encoding="utf-8"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
  <meta content="text/html; charset=UTF-8" http-equiv="content-type" />
  <title>Savonet / Liquidsoap 1.3.0 :: Multimedia Stream Generation</title>
  <link href="../css/new.css" type="text/css" rel="stylesheet" />
  <link href="//ajax.googleapis.com/ajax/libs/jqueryui/1.8.17/themes/base/jquery-ui.css" type="text/css" rel="stylesheet" />
  <script type="text/javascript" src="//www.google.com/jsapi"></script>
  <link rel="shortcut icon" href="../favicon.ico" />
  <script>
    google.load("jquery", "1.6.1");
    google.load("jqueryui", "1.8.14");
  </script>
  <script type="text/javascript" src="../js/jQuery.syntax/jquery.syntax.min.js"></script>
  <script type="text/javascript" src="../js/liq-jquery.js"></script>
</head>
<body>
  <div class="menu">
    <ul>
      <li id="menu-developers">
        <a href="https://github.com/savonet/liquidsoap/issues" target="_blank">developers</a></li>
      <li id="menu-flows"><a href="https://flows.liquidsoap.info/">Flows!</a></li>
      <li id="menu-doc-api-www">
        <a href="reference.html">API</a></li>
      <li id="menu-support">
        <a href="documentation.html">documentation</a></li>
      <li id="menu-download">
        <a href="../download.html">download</a></li>
      <li id="menu-about">
        <a href="../index.html">about</a></li>
    </ul>
  </div>
  <div class="section" id="main">
    <h1>Liquidsoap 1.3.0 : Dolebraï</h1>
    <div class="content">
      <h3>Dolebraï</h3>
<p>
<a href="http://dolebrai.net" target="_blank">Dolebraï</a> is a netradio dedicated to free music,
<em>i.e.</em> music distributed under Creative-Commons-alike licenses.
Its goal is to advertise this alternative way of distributing music
by broadcasting a selection of quality tracks:
<a href="http://dolebrai.net:8000/dolebrai.ogg" target="_blank">listen to it</a>!
</p>
<p>
More information about the project can be found on its
<a href="http://dolebrai.net" target="_blank">website</a> (still only in french).
As an example of a (now not so) simple liquidsoap configuration,
here is the script used on that radio.
</p>
<pre class="syntax liq">#!/home/dbaelde/bin/liquidsoap -v

set("root.max_latency",30.)
set("server.telnet",true)
set("server.socket.permissions",0o660)
set("tag.encodings",["UTF-8","ISO-8859-1"])

%include "dolutils.liq"

root     = "/var/cache/dolebrai/"
scripts  = "/home/dbaelde/scripts/"
last_xml = "/home/dbaelde/dolebrai_public_html/last.xml"
default  = "/var/cache/dolebrai/local/gelnior/jazz/07-patamouchto.mp3"

add_protocol("dolebrai",
  fun (arg,delay) -&gt;
    get_process_lines("#{scripts}dolebrai-filename #{quote(arg)}"))

# === MAIN SOURCE ===

# play explicit requests (from admins) or the playlist

default = single(id="default",default)

s =
  request.dynamic(id="scheduler",default_duration=30.,length=1.,
    fun () -&gt;
      request(get_process_output("#{scripts}/scheduler.pl 2&gt;&gt; #{scripts}/log")))

s = fallback([ request.queue(id="q"), s, default ])

# Feedback is provided for any file played, be it from scheduler.pl, user 
# request via the queue or the default file.
# By the way we refresh the XML describing the latest tracks.
def feedback(m)
  system("#{scripts}/feedback.pl #{quote(m['filename'])} 2&gt;&gt; #{scripts}/logf")
  system("#{scripts}/last.rb &gt; #{last_xml}")
end
# Add a telnet control to get info before metadata rewriting, used by last.rb
s = store_metadata(size=20,id="history",s)
s = on_metadata(feedback,s)

# Prepend introductory tracks if necessary
intro_mix = single("/var/cache/dolebrai/local/jingles/in_vitro/DJLEmb2.mp3")
def choose_prepend(m)
  if m["prepend_track"] != "" then
    request.queue(interactive=false,queue=[request(m["prepend_track"])])
  elsif m["track_type"] == "mix" then
    # This is more reliable than prepend_track as intro_mix is always ready
    intro_mix
  else
    fallback([])
  end
end
s = prepend(s, choose_prepend)

# === TWEAKS (blanks & metadata) ===

# Cut blanks (bonus-tracks)
s = skip_blank(length=7.,s)

# Rewrite metadata
s = rewrite_metadata([("title","$(title) (Dolebrai)"),
                      ("comment","http://dolebrai.net")],s)

# === JINGLES ===

# Metadata rewriting wrapper for jingle sources
def jingle_meta(j)
  rewrite_metadata([("artist", "Dolebrai"),
                    ("title","Jingle (Dolebrai)"),
                    ("comment","http://dolebrai.net")],j)
end

# One source for static jingles, added between tracks
jingles_stat = jingle_meta(playlist.safe(reload=3600,root^"jingles.static"))

# Add cross-fading, inserting "static" jingles during the fade..
def fade(jingles,s)
  fade_in = 2.
  fade_out = 2.
  start_next = 2.
  j = random(id="jrand",
             strict=true,weights=[1,5],
             [jingles, blank(id="bbb",duration=1.)])
  def fader(from,to)
    from = fade.out(duration=fade_out,from)
    to = fade.in(duration=fade_in,to)
    add(normalize=false,
        [ to,
          sequence([ j, fallback([]) ]),
          from ])
  end
  cross(duration=start_next,fader,s)
end
# s = fade(jingles_stat,s)
# Use the fader defined in dolutils.liq
# The smart fade tends to loose some metadata because there is no handling of 
# them in the generators...
s = dole_fade(jingles=jingles_stat,s)

# One for dynamic jingles, mixed on top of the stream with smooth starts/ends.
jingles_dyn = jingle_meta(playlist(reload=3600,root^"jingles.dynamic"))
jingles_dyn = fallback([request.queue(id="dynq"),jingles_dyn])
s = smooth_add(normal=s,special=switch([({ 0m0s or 30m0s }, jingles_dyn)]))

# === ADMIN INTERACTION ===
# More interactivity to come, included live show hooks & co
# s = mix([s,request.equeue(id="eq")])

# === OUTPUT ===
out_mp3 = output.icecast(%mp3, 
    password="XXX", bitrate=64, samplerate=22050, quality=2,
    genre="Musique Libre",
    url="http://dolebrai.net",
    description="La radio des musiques libres")
out_vorbis = output.icecast(%vorbis, 
    password="XXX",
    genre="Musique Libre",
    description="La radio des musiques libres")

# s = normalize(s)
s = audio_process(s)

out_vorbis(port=8000,mount="dolebrai.ogg",s)
out_mp3(port=8000,mount="dolebrai.mp3",s)

# out(port=7144,s) # One for peercast

# Start a watchdog to tell us if liquidsoap crashes
system("#{scripts}watchdog-dolebrai &")
</pre>
<div align="right">
<a href="scripts/dolebrai.liq">
<img class="grab" src="../images/grab.png" alt="Grab the code!">
</a>
</div></p>
<p>
Finally our <code>dolutils.liq</code> library, included from the main script, 
is as follows.
</p>
<pre class="syntax liq">def dole_fade (~start_next=3.,~fade_in=3.,~fade_out=3.,
                     ~width=1.,~conservative=false,~jingles,s)
  mf_high = 20.
  mf_medium = 32.
  mf_margin = 4.
  fade.out = fade.out(duration=fade_out)
  fade.in = fade.in(duration=fade_in)

  j = random(id="jrand",
             strict=true,weights=[1,5],
             [jingles, blank(id="bbb",duration=1.)])
  add = fun (from,to) -&gt;
    add(normalize=false,
        [ to,
          sequence([ j, fallback([]) ]),
          from ])

  log = log(label="dole_fade")

  def transition (a, b, ma, mb, sa, sb)
    if
      (a + mf_high &gt;= 0. and b + mf_high &gt;= 0.) or
      (a + mf_medium &gt;= 0. and b + mf_medium &gt;= 0. and abs(a - b) &lt;= mf_margin)
    then
      log("No transition, just sequencing.")
      sequence([sa, sb])

    elsif
      a + mf_medium &lt;= 0. and b + mf_medium &gt;= 0. and abs(a - b) &lt;= mf_margin
    then
      log("Using transition 1: crossed.")
      add(fade.out(sa),fade.in(sb))

    elsif
      b &gt;= a + mf_margin and a &lt;= mf_high
    then
      log("Using transition 2: crossed, fade-out.")
      add(fade.out(sa),sb)

    elsif
      a &gt;= b + mf_margin and b &lt;= mf_high
    then
      log("Using transition 3: crossed, fade-in.")
      add(sa,fade.in(sb))

    else
      log("Using transition 4: crossed, fade-in, fade-out.")
      add(fade.out(sa),fade.in(sb))
    end
  end

  smart_cross(transition,
               width=width,
               duration=start_next,conservative=conservative,
               s)
end

def audio_process(s)
  gain = interactive.float("compress.gain",3.)
  ratio = 3. # interactive.float("compress.ratio",3.)
  threshold = interactive.float("compress.threshold",-15.)

  target = interactive.float("norm.target",-13.)
  k_up = interactive.float("norm.kup",0.005)
  k_down = interactive.float("norm.kdn",0.1)

  s = normalize(target=target,k_up=k_up,k_down=k_down,s)
  s = compress(gain=gain,ratio=ratio,threshold=threshold,s)
  s
end
</pre>
<div align="right">
<a href="scripts/dolutils.liq">
<img class="grab" src="../images/grab.png" alt="Grab the code!">
</a>
</div></p>
      <div id="footer"> 2003-2017 Savonet team</div>
    </div>
  </div>
  <script type="text/javascript" src="//www.google.com/cse/brand?form=cse-search-box&amp;lang=en"></script>
  <script type="text/javascript" src="../js/search.js"></script>
  <script type="text/javascript">
    var _gaq = _gaq || [];
    _gaq.push(['_setAccount', 'UA-927362-1']);
    _gaq.push(['_trackPageview']);
    (function() {
    var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
    ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
    })();
  </script>
</body>
</html>
